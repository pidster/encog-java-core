apply plugin: 'build-dashboard'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'jacoco'
apply plugin: 'sonar-runner'

sourceCompatibility=1.6
targetCompatibility=1.6

def props = new Properties();
def localProperties = new File("local.properties")
if (localProperties.exists()) localProperties.withInputStream { props.load(it) }

group = props.get('group')
version = props.get('version')

configurations {
    deployerJars
}

repositories {
   mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.10'
    testCompile 'org.hsqldb:hsqldb:2.0.0'

    deployerJars 'org.apache.maven.wagon:wagon-ssh:2.1'
}

task sourcesJar(type: Jar, dependsOn: [ 'classes' ]) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: [ 'javadoc' ]) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

// configure the build dashboard to run the build task
// buildDashboard.finalizedBy build
build.mustRunAfter buildDashboard

// always trigger the JaCoCo test report on full checks, but not the test task
check.finalizedBy jacocoTestReport

// Announce the test results pages
test.doLast {
    println "Unit Test Results Report: ${reports.html.destination}/index.html"
}
jacocoTestReport.doLast {
    println "JaCoCo Coverage Report  : ${reports.html.destination}/index.html"
}

build.doLast {
    println "Build Dashboard: ${buildDashboard.reports.html.destination}/index.html"
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

sonarRunner {
    sonarProperties {
        // property "sonar.host.url", "${sonarHostUrl}"
        // property "sonar.jdbc.url", "${sonarJdbcUrl}"
        // property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
        // property "sonar.jdbc.username", "${sonarJdbcUsername}"
        // property "sonar.jdbc.password", "${sonarJdbcPassword}"
        property "sonar.junit.reportsPath", "build/test-results"
        property "sonar.jacoco.reportPath", "build/jacoco/test.exec"
        property "sonar.jacoco.itReportPath", "build/jacoco/integTest.exec"
        // property 'sonar.tests', 'src/test/java'
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: props.get('maven.repository.url')) {
                authentication(userName: props.get('maven.repository.user'), password: props.get('maven.repository.password'))
            }
        }
    }
}
